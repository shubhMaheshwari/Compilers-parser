class Program {
  int input[100], temp[100];
  void merge(int l, int r) {
    int mid, i, j;

    if (l >= r) {
      return;
    }

    mid = (l + r)/2;
    merge(l, mid);
    merge(mid + 1, r);

    i = l;
    j = mid + 1;
    for k = l, r + 1 {
      if (j > r) {
        temp[k] = input[i];
        i += 1;
      } else {
        if (i > mid || input[j] < input[i]) {
          temp[k] = input[j];
          j += 1;
        } else {
          temp[k] = input[i];
          i += 1;
        }
      }
    }
    
    for k = l, r + 1 {
      input[k] = temp[k];
    }
  }

  int getInt() {
    int ret, input;
    ret = 0;
    for thisLanguageHasNoWhileLoop = 1, 2 {
      thisLanguageHasNoWhileLoop -= 1;

      input = callout("getchar");
      if (input >= 48 && input <= 57) {
        ret = 10*ret + input - 48;
      } else {
        break;
      }
    }
    return ret;
  }
  int main() {
    int n;
    n = getInt();
    for i = 0, n {
      input[i] = getInt();
    }
    merge(0, n - 1);
    for i = 0, n {
      callout("printf", "%d ", input[i]);
    }
    return 0;
  }
}
